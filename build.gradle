plugins {
	id 'java'
	id 'com.diffplug.eclipse.apt' version '3.22.0'
	id 'org.seasar.doma.compile' version '1.0.0'
	id 'org.springframework.boot' version '2.2.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

group = 'domaboot'
//version = '2.17.1-SNAPSHOT'

def domaVersion = project.property('release.releaseVersion')

java{
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
    // disable the annotation processors during the test run
    options.compilerArgs = ['-proc:none']
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://repo.spring.io/libs-snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.3.0'
    //domaの注釈処理を実行することを示す 必須
    annotationProcessor "org.seasar.doma:doma-processor:${domaVersion}"
    // domaへの依存を示す
    implementation "org.seasar.doma:doma-core:${domaVersion}"
    runtimeOnly 'com.h2database:h2'
    //testImplementation 'junit:junit'
    //testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                classpath.entries.removeAll { it.path == '.apt_generated' }
            }
            withXml { provider ->
                def node = provider.asNode()
                // specify output path for .apt_generated
                node.appendNode( 'classpathentry', [ kind: 'src', output: 'bin/main', path: '.apt_generated'])
            }
        }
    }
    jdt {
        javaRuntimeName = 'JavaSE-11'
    }
}
